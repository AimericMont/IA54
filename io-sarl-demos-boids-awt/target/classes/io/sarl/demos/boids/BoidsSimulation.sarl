/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2020 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.demos.boids

import io.sarl.bootstrap.SRE
import io.sarl.bootstrap.SREBootstrap
import io.sarl.demos.boids.gui.EnvironmentGui
import io.sarl.lang.core.AgentContext
import io.sarl.lang.core.Event
import io.sarl.lang.core.EventListener
import io.sarl.core.OpenEventSpace
import java.util.Map
import java.util.UUID
import java.util.concurrent.ConcurrentHashMap
import java.util.concurrent.ConcurrentLinkedQueue
import org.arakhne.afc.math.geometry.d2.d.Vector2d
import java.util.Random
import java.util.Date

/** 
 * The boids simulation launching the SARL environment with the corresponding agent and ensuring the communication between agents and the GUI
 * @author Nicolas Gaud
 */
class BoidsSimulation implements EventListener {

	public static val id = UUID::randomUUID

	/** 
	 * SRE Kernel instance
	 */
	var kernel : SREBootstrap

	/** 
	 * The default SARL context where environment and boids are spawned
	 */
	var defaultSARLContext : AgentContext

	/** 
	 * Identifier of the environment
	 */
	var environment : UUID

	var width = Settings::EnvtWidth

	var height = Settings::EnvtHeight

	/** 
	 * Map buffering boids before launch/start
	 */
	var boidsToLaunch : Map<Population, Integer>

	/** 
	 * Map buffering boids' bodies before launch/start
	 */
	var boidBodies : ConcurrentHashMap<UUID, PerceivedBoidBody>

	var boidsCount : int

	var placeQueue : int

	var queue1 : int = 0
	var queue2 : int = 0
	var queue3 : int = 0
	var queue4 : int = 0

	/** 
	 * Boolean specifying id the simulation is started or not.
	 */
	var isSimulationStarted = false

	/** 
	 * the vent space used to establish communication between GUI and agents,
	 * Especially enabling GUI to forward start event to the environment, 
	 * respectively the environment to send GUIRepain at each simulation step to the GUI
	 */
	var ^space : OpenEventSpace

	/** 
	 * The Graphical user interface
	 */
	var myGUI : EnvironmentGui

	var seatList = new ConcurrentLinkedQueue<Integer>()

	new {
		boidsCount = 0
		placeQueue = 0

		this.boidBodies = new ConcurrentHashMap

		boidsToLaunch = newHashMap
		var j = -1
		for (var i = 0; i < BoidsSimulationLauncher::NB_X_SEAT * BoidsSimulationLauncher::NB_Y_SEAT; i++) {

			if (BoidsSimulationLauncher::scenario == 2 || BoidsSimulationLauncher::scenario == 3) {
				if (i % BoidsSimulationLauncher::NB_X_SEAT == 0) {
					j = j + 1
				}
				if (j % 2 == 0) {
					if (i % 2 == 1)
						seatList.add(i)
				} else {
					if (i % 2 == 0)
						seatList.add(i)
				}
			} else {
				seatList.add(i)
			}

		}
	}

	def start : void {
		launchAllAgents
		isSimulationStarted = true
		kernel.startAgentWithID(typeof(Chart), UUID::randomUUID)
		BoidsSimulationLauncher::initTime = new Date()
		BoidsSimulationLauncher::startTime = true
	}

	def stop : void {
		killAllAgents
		isSimulationStarted = false
	}

	/** 
	 * Add the boids of a population to the simulation.
	 * 
	 * @param p - the population to add.
	 */
	def addBoid(p : Population) : void {
		boidsCount++
		if (!isSimulationStarted) {
			var currentBoidCount = boidsToLaunch.get(p)
			if (currentBoidCount !== null) {
				currentBoidCount++
			} else {
				currentBoidCount = new Integer(1)
			}
			boidsToLaunch.put(p, currentBoidCount)
		} else {
			launchBoid(p, "Boid" + boidsCount)
		}
	}

	private def launchAllAgents : void {

		kernel = SRE::getBootstrap

		defaultSARLContext = kernel.startWithoutAgent

		environment = UUID::randomUUID
		kernel.startAgentWithID(typeof(Environment), environment, height, width)

		launchAllBoids
		^space = defaultSARLContext.defaultSpace as OpenEventSpace

		this.myGUI = new EnvironmentGui(^space, height, width, this.boidBodies)

		// Registering to receive GUIRepaint events
		^space.register(this)
		// Sending start to Environment
		^space.emit(id, new Start(this.boidBodies))
	}

	private def launchAllBoids : void {
		var boidNum = 0
		for (e : boidsToLaunch.entrySet) {
			for (var i = 0; i < e.value; i++) {
				boidNum++
				e.key.launchBoid("Boid" + boidNum)
			}
		}
	}

	@SuppressWarnings("constant_condition", "discouraged_reference")
	private def launchBoid(p : Population, boidName : String) : void {

		var px : int
		var py : int
		var nbyseat : int = BoidsSimulationLauncher::NB_Y_SEAT

		placeQueue++
		var catering : boolean
		var seatNumber : int
		var tmp : int
		var compteur : int = 0
		var r = new Random()
		var seatGroup : int
		tmp = r.nextInt(seatList.size())
		for (elem : seatList) {
			if (tmp == compteur) {
				seatNumber = elem
			}
			compteur++
		}
		seatList.remove(seatNumber)
		var guichet : int

		var tmpNBBois = BoidsSimulationLauncher::NB_X_SEAT * BoidsSimulationLauncher::NB_Y_SEAT

		if (seatNumber < tmpNBBois / 4)
			seatGroup = 1
		else if (seatNumber <= tmpNBBois / 2)
			seatGroup = 2
		else if (seatNumber < tmpNBBois / 4 * 3)
			seatGroup = 3
		else
			seatGroup = 4

		if (BoidsSimulationLauncher::scenario == 1) {
			px = 275 + placeQueue % nbyseat * 7
			py = 60 - 18 + placeQueue / nbyseat * 10
			guichet = 3
		} else if (BoidsSimulationLauncher::scenario == 2) {
			if (seatGroup == 1 || seatGroup == 2) {
				px = 275 + queue1 % nbyseat * 7
				py = - 60 - 18 + queue1 / nbyseat * 10
				queue1++
				guichet = 2
			} else {
				px = 275 + queue2 % nbyseat * 7
				py = 60 - 18 + queue2 / nbyseat * 10
				queue2++
				guichet = 3
			}
		} else if (BoidsSimulationLauncher::scenario == 3) {
			if (seatGroup == 1) {
				px = 275 + queue1 % (nbyseat / 2) * 10
				py = - 180 - 18 + queue1 / (nbyseat / 2) * 10
				queue1++
				guichet = 1
			} else if (seatGroup == 2) {
				px = 275 + queue2 % (nbyseat / 2) * 10
				py = - 60 - 18 + queue2 / (nbyseat / 2) * 10
				queue2++
				guichet = 2
			} else if (seatGroup == 3) {
				px = 275 + queue3 % (nbyseat / 2) * 10
				py = 60 - 18 + queue3 / (nbyseat / 2) * 10
				queue3++
				guichet = 3
			} else {
				px = 275 + queue4 % (nbyseat / 2) * 10
				py = 180 - 18 + queue4 / (nbyseat / 2) * 10
				queue4++
				guichet = 4
			}
		}

		var initialVitesse = new Vector2d(Math::random - 0.5, Math::random - 0.5)
		var initialPosition = new Vector2d(px, py)

		tmp = r.nextInt(100)
		if (tmp >= 70)
			catering = true
		else
			catering = false

		/* tmp = (Math::random) as int
		 * if (tmp >0.7)
		 * catering = true
		 * else
		 * catering = false
		 */
		var b = UUID::randomUUID
		kernel.startAgentWithID(typeof(Boid), b, environment, p, initialPosition, initialVitesse, boidName, seatNumber,
			seatGroup, catering)
		this.boidBodies.put(b, new PerceivedBoidBody(p, b, initialPosition, initialVitesse, catering, guichet))

		if (Settings::isLogActivated) {
			System.out.println("Lancement d'un boid Ã  la position " + initialPosition + " et avec une vitesse de " +
				initialVitesse)
		}
	}

	private def killAllAgents : void {
		// do something if you want to
	}

	override getID : UUID {
		id
	}

	/** 
	 * Methods managing event coming from agents 
	 */
	override receiveEvent(^event : Event) {
		if (^event instanceof GuiRepaint) { // event from the environment, GUI must be refreshed
			this.myGUI.boids = ^event.perceivedAgentBody
			this.myGUI.repaint
		}
	}

}
